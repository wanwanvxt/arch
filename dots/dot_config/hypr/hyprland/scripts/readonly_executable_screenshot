#!/usr/bin/env fish

set -l lock_file "/tmp/hyprland_script_screenshot.lock"

function remove_lock_file --inherit-variable lock_file
    rm -f "$lock_file"
end
trap remove_lock_file EXIT
test -f "$lock_file" && exit 1
touch "$lock_file"

## --- --- ---

set -l screenshots_dir "$(xdg-user-dir PICTURES)/screenshots"
mkdir -p "$screenshots_dir"

set -l file_name "screenshot_$(date +'%Y-%m-%d_%H%M%S.png')"
set -l file_path "$screenshots_dir/$file_name"

function opts
    fish_opt -s m -l mode --required-val
end
argparse (opts) -- $argv
set -q _flag_mode || exit 1

set -l mode (echo $_flag_mode | tr '[:upper:]' '[:lower:]')
switch $mode
    case monitor screen
        set -l monitor (hyprctl -j monitors | jq -r '.[] | select(.focused) | .name')
        grim -o "$monitor" - | tee "$file_path" | wl-copy
    case window
        set -l box (hyprctl -j activewindow | jq -r '"\(.at[0]),\(.at[1]) \(.size[0])x\(.size[1])"')
        grim -g "$box" - | tee "$file_path" | wl-copy
    case area
        grim -g (slurp -d) - | tee "$file_path" | wl-copy
    case "*"
        exit 1
end
