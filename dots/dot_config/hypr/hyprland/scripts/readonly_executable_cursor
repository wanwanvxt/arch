#!/usr/bin/env fish

if test -n "$XDG_CONFIG_HOME"
    set xdg_cfg_home $XDG_CONFIG_HOME
else
    set xdg_cfg_home "$HOME/.config"
end

set gtk_cfg_file "$xdg_cfg_home/gtk-4.0/settings.ini"
test -f "$gtk_cfg_file"; or set gtk_cfg_file "$xdg_cfg_home/gtk-3.0/settings.ini"
test -f "$gtk_cfg_file"; or exit 1

function get_gtk_value -a key -V gtk_cfg_file
    set -l line (grep "$key" "$gtk_cfg_file")
    test -n "$line"; and string trim (string split -m1 '=' $line)[2]
end

set cursor_theme_name "$(get_gtk_value gtk-cursor-theme-name)"
set cursor_theme_size "$(get_gtk_value gtk-cursor-theme-size)"

hyprctl setcursor "$cursor_theme_name" "$cursor_theme_size"
printf "%s\n" "Xcursor.theme: $cursor_theme_name" "Xcursor.size: $cursor_theme_size" >"$HOME/.Xresources"
xrdb -merge "$HOME/.Xresources"
